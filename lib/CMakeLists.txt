SET (LIB_OUTPUT_NAME cardano-c)

# Find external packages
SET (sodium_USE_STATIC_LIBS ON)
SET (jsonc_USE_STATIC_LIBS ON)

FIND_PACKAGE (Sodium REQUIRED)
FIND_PACKAGE (Jsonc REQUIRED)

INCLUDE_DIRECTORIES (SYSTEM ${sodium_INCLUDE_DIR})
INCLUDE_DIRECTORIES (SYSTEM ${jsonc_INCLUDE_DIR})
INCLUDE_DIRECTORIES (SYSTEM ./include)

IF (UNIX AND NOT APPLE)
  SET (RT_LIBRARIES rt)
ELSE ()
  SET (RT_LIBRARIES)
ENDIF ()

# Create configuration header
CONFIGURE_FILE ("${CMAKE_CURRENT_SOURCE_DIR}/src/config.h.in" "${CMAKE_CURRENT_SOURCE_DIR}/src/config.h")
CONFIGURE_FILE ("${CMAKE_CURRENT_SOURCE_DIR}/${LIB_OUTPUT_NAME}.pc.in" "${CMAKE_CURRENT_BINARY_DIR}/${LIB_OUTPUT_NAME}.pc")

# Source code files
FILE (GLOB_RECURSE HEADER_FILES include/*.h include/*.inl)
FILE (GLOB_RECURSE SOURCE_FILES src/*.c include/*.h include/*.inl)
FILE (GLOB_RECURSE TEST_SRC_FILES tests/*.cpp tests/*.h tests/*.inl)

if (NOT DISABLE_STATIC_FPIC)
  # Use '-fPIC'/'-fPIE' option.
  SET(CMAKE_POSITION_INDEPENDENT_CODE ON)
endif()

SET(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
SET(CMAKE_EXPORT_COMPILE_COMMANDS ON)

OPTION(BUILD_SHARED_LIBS  "Default to building shared libraries" ON)
OPTION(BUILD_STATIC_LIBS  "Default to building static libraries" OFF)

if (BUILD_SHARED_LIBS)
  ADD_DEFINITIONS(-D CARDANO_C_DLL)
endif()

ADD_DEFINITIONS(-D SODIUM_STATIC)
ADD_DEFINITIONS(-D JSONC_STATIC)

# Compiler options
IF (CMAKE_COMPILER_IS_GNUC)
  SET (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -std=c99 -pedantic-errors -Wall -Wextra -Werror -Wswitch-enum -DHAVE_CONFIG_H")
ENDIF ()

# Build library
ADD_LIBRARY (${LIB_OUTPUT_NAME} SHARED ${SOURCE_FILES})

TARGET_LINK_LIBRARIES (${LIB_OUTPUT_NAME}
  PRIVATE ${sodium_LIBRARY_RELEASE}
  PRIVATE ${jsonc_LIBRARY_RELEASE}
)

# Allow to build static and shared libraries at the same time
if (BUILD_STATIC_LIBS AND BUILD_SHARED_LIBS)
  SET(STATIC_LIB ${PROJECT_NAME}-static)
  ADD_LIBRARY(${STATIC_LIB} STATIC ${SOURCE_FILES})
  TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}-static
    PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>
    $<BUILD_INTERFACE:${PROJECT_BINARY_DIR}>
  )

  TARGET_LINK_LIBRARIES(${PROJECT_NAME}-static
    PRIVATE ${sodium_LIBRARY_RELEASE}
    PRIVATE ${jsonc_LIBRARY_RELEASE}
  )

  # rename the static library
  if (NOT MSVC)
    SET_TARGET_PROPERTIES(${STATIC_LIB} PROPERTIES
            OUTPUT_NAME ${PROJECT_NAME}
    )
  endif()
  LIST(APPEND CMAKE_TARGETS ${STATIC_LIB})
endif ()

# Code Coverage
IF (CMAKE_BUILD_TYPE STREQUAL "Debug")
  SET(GCC_COVERAGE_COMPILE_FLAGS "-fprofile-arcs -ftest-coverage")
  SET(GCC_COVERAGE_LINK_FLAGS "-lgcov")
  SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${GCC_COVERAGE_COMPILE_FLAGS}")
  SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}")
ENDIF ()

IF (CMAKE_BUILD_TYPE STREQUAL "Fuzz")
  # Set compiler to Clang explicitly
  set(CMAKE_C_COMPILER "clang")
  set(CMAKE_CXX_COMPILER "clang++")

  # Define additional flags for fuzzing
  set(CLANG_COMPILE_FLAGS "-g -O1 -fsanitize=fuzzer,address -fprofile-instr-generate -fcoverage-mapping -fprofile-arcs -ftest-coverage")
  set(CLANG_LINK_FLAGS "-fsanitize=fuzzer,address -fprofile-instr-generate --coverage")

  # Append the fuzzing flags to the existing flags
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${CLANG_COMPILE_FLAGS}")
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${CLANG_COMPILE_FLAGS}")
  set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${CLANG_LINK_FLAGS}")
ENDIF ()

# Pre-compiled header support
IF (COTIRE_ENABLED)
  INCLUDE (Cotire)
  COTIRE (${LIB_OUTPUT_NAME})
ENDIF ()

INSTALL (
  TARGETS     ${LIB_OUTPUT_NAME}
  DESTINATION lib
  COMPONENT   lib
  PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ GROUP_WRITE WORLD_READ WORLD_WRITE
)

INSTALL (
  TARGETS     ${STATIC_LIB}
  DESTINATION lib
  COMPONENT   lib
  PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ GROUP_WRITE WORLD_READ WORLD_WRITE
)

INSTALL (
  DIRECTORY   ${CMAKE_CURRENT_SOURCE_DIR}/include/cardano
  DESTINATION include
  COMPONENT   dev
  PATTERN     "*.h"
  PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ GROUP_WRITE WORLD_READ
)

INSTALL (
  DIRECTORY   ${CMAKE_CURRENT_SOURCE_DIR}/include/cardano
  DESTINATION include
  COMPONENT   dev
  PATTERN     "*.inl"
  PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ GROUP_WRITE WORLD_READ
)

INSTALL (
  FILES         ${CMAKE_CURRENT_BINARY_DIR}/${LIB_OUTPUT_NAME}.pc
  DESTINATION   lib/pkgconfig
  COMPONENT     dev
  NAMELINK_SKIP PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ GROUP_WRITE WORLD_READ WORLD_WRITE
)

# Build tests
IF (TESTING_ENABLED AND TEST_SRC_FILES)
  SET (PROJECT_TEST_NAME test-${LIB_OUTPUT_NAME})
  PROJECT (PROJECT_TEST_NAME CXX)

  FIND_PACKAGE (GTest       REQUIRED)
  FIND_PACKAGE (GoogleMocks REQUIRED)

  ADD_EXECUTABLE (${PROJECT_TEST_NAME} ${TEST_SRC_FILES})

  TARGET_LINK_LIBRARIES (${PROJECT_TEST_NAME}
    PRIVATE ${LIB_OUTPUT_NAME}
    PRIVATE ${GTEST_MAIN_LIBRARY}
    PRIVATE ${GTEST_LIBRARY}
    PRIVATE ${GTEST_MAIN_LIBRARIES}
    PRIVATE ${GTEST_LIBRARIES}
    PRIVATE ${GoogleMocks_LIBRARIES}
  )

  TARGET_INCLUDE_DIRECTORIES (${PROJECT_TEST_NAME} SYSTEM PRIVATE ${GTEST_INCLUDE_DIR})
  TARGET_INCLUDE_DIRECTORIES (${PROJECT_TEST_NAME} SYSTEM PRIVATE ${GoogleMocks_INCLUDE_DIRS})
  TARGET_INCLUDE_DIRECTORIES (${PROJECT_TEST_NAME} SYSTEM PRIVATE "${CMAKE_CURRENT_SOURCE_DIR}/include" "${CMAKE_CURRENT_SOURCE_DIR}/src")

  GTEST_ADD_TESTS ("${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${PROJECT_TEST_NAME}" "" ${TEST_SRC_FILES})
ENDIF ()
